apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco-android'

jacoco {
    toolVersion = "0.8.5"
}

android {

    compileSdkVersion 28
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            useProguard false
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'org/apache/http/version.properties'
        exclude 'org/apache/http/client/version.properties'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        animationsDisabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api('org.smartregister:opensrp-client-core:1.11.3-SNAPSHOT@aar') {
        transitive = true
        exclude group: 'com.github.bmelnychuk', module: 'atv'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.rengwuxian.materialedittext', module: 'library'
    }

    api("org.smartregister:android-p2p-sync:0.3.3-SNAPSHOT") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'org.apache.httpcomponents', module: 'httpmime'
        exclude group: 'org.smartregister', module: 'opensrp-client-native-form'
    }

    api("com.nerdstone:neat-form-core:1.1.0-Testing") {
        transitive = true
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'androidx.appcompat', module: 'appcompat'
        exclude group: 'androidx.constraintlayout', module: 'constraintlayout'
    }

    api('org.smartregister:opensrp-client-chw-anc:0.1.39-SNAPSHOT@aar') {
        transitive = true
        exclude group: 'org.smartregister', module: 'opensrp-client-core'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'org.smartregister', module: 'opensrp-client-native-form'
        exclude group: 'org.smartregister', module: 'opensrp-client-configurable-views'
        exclude group: 'org.smartregister', module: 'opensrp-client-immunization'
    }

    // Koin for Android dependency injection
    api "org.koin:koin-android:$koinVersion"
    api "org.koin:koin-androidx-scope:$koinVersion"
    api "org.koin:koin-androidx-viewmodel:$koinVersion"

    implementation('org.smartregister:opensrp-client-configurable-views:1.1.2-SNAPSHOT@aar') {
        transitive = true
        exclude group: 'org.smartregister', module: 'opensrp-client-core'
        exclude group: 'org.smartregister', module: 'opensrp-client-native-form'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    // compatibility issue when upgraded to 1.1.*
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.2'
    // compatibility issue when upgraded to 1.1.*
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.reflection-utils:reflection-utils:0.0.1'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    // compatibility issue when upgraded to 2.2.*
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    // compatibility issue when upgraded to 24.*
    implementation 'com.google.guava:guava:20.0'
    implementation 'id.zelory:compressor:1.0.4'
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'
    implementation "androidx.core:core-ktx:1.1.0"

    testImplementation "org.koin:koin-test:$koinVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation "org.robolectric:shadows-multidex:4.3.1"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation 'com.agoda.kakao:kakao:2.2.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'

    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.2.0'

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.1.0'
    }
}

tasks.coveralls {
    dependsOn 'jacocoTestReport'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestDebugUnitTestReport/jacocoTestDebugUnitTestReport.xml"
}

//For Kotlin documentation more here https://github.com/Kotlin/dokka/blob/master/README.md
dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"

    configuration {
        moduleName = 'opensrp-chw-referral'
        reportUndocumented = true
        // Suppress a package
        perPackageOption {
            prefix = "kotlin.internal" // will match kotlin.internal and all sub-packages of it
            suppress = true
        }
    }

}
apply from: '../maven.gradle'

